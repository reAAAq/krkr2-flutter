plugins {
    id 'com.android.application'
}

android {
    ndkVersion '27.2.12479018'
    compileSdk PROP_COMPILE_SDK_VERSION.toInteger()
    namespace 'org.github.krkr2'

    defaultConfig {
        applicationId 'org.github.krkr2'
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION

        ndk {
            jobs Runtime.runtime.availableProcessors()
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a' //, 'armeabi-v7a'
        }

        externalNativeBuild {
            cmake {
                def buildTypeName = {
                    return project.gradle.startParameter.taskNames.any { it.toLowerCase().contains('release') } ? "Release" : "Debug"
                }
                targets 'krkr2'
                arguments "-DCMAKE_BUILD_TYPE=${buildTypeName()}"
                cppFlags '-fsigned-char'
            }
        }
        versionCode Integer.parseInt(new Date().format('yyyyMMddHH'))
        versionName '1.4.0'
    }

    signingConfigs {
        release {
            storeFile file('sign.keystore')
            def signPropsFile = file('sign.properties')
            Properties signProps = new Properties()
            signProps.load(new FileInputStream(signPropsFile))
            keyAlias signProps['SIGN_KEY_ALIAS']
            keyPassword signProps['SIGN_KEY_PASS']
            storePassword signProps['SIGN_STORE_PASS']
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            debuggable true
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['java']
            res.srcDirs = ['res']
//            jniLibs.srcDirs = ['jniLibs']
            assets.srcDirs = ['../../ui/cocos-studio']
        }

    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
        cmake {
            buildStagingDirectory '../../out/cmake-build-android'
            path '../../CMakeLists.txt'
        }
    }

    splits {
        abi {
            enable = true           // 启用 ABI 分包
            reset()               // 清除默认支持的 ABI
            //noinspection ChromeOsAbiSupport
            include 'arm64-v8a' // 'armeabi-v7a', 'x86', 'x86_64' // 指定支持的 ABI
            universalApk = true
        }
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def ver = defaultConfig.versionName
            def abi = filters.find { it.filterType == "ABI" }?.identifier ?: "all"
            outputFileName = "${rootProject.name}-$ver-${abi}.apk"
        }
    }
}

dependencies {
    //implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':cocos2dx')

    implementation 'androidx.activity:activity:1.9.3'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.fragment:fragment:1.8.5'

    configurations.configureEach {
        resolutionStrategy {
            force 'org.jetbrains.kotlin:kotlin-stdlib:1.8.22'
            force 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22'
        }
    }
}
